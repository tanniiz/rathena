//===== rAthena Script =======================================
//= Black Jack
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Black Jack card game.  Gameplay based off standard casino
//= black jack rules.  Dealer must have at least 17 to stay and will
//= automatically stay at 17 and up.  Player must have at least
//= 13 to stay.  Aces counted as 11 or 1.  Option to "Double Down".
//= Currently does not allow for "insurance", or "splitting"
//= of pairs.
//===== Additional Comments: =================================
//= 1.0 First version.
//============================================================

morocc,154,88,1	script	Black Jack Dealer	57,{
	mes "[Dealer]";
	mes "สวัสดีผู้เล่นเจ้าต้องการเล่น blackjack ไหม?";
M_Menu:
	next;
	menu "ดูกติกา",M_0, "ฉันต้องการเล่น",L_Play, "ยกเลิก",M_End;

M_0:
	mes "[Dealer]";
	mes "เจ้าต้องการรู้อะไรหละ?";
sM_Menu0:
	next;
	menu "พื้นฐาน",sM_0a, "การแพ้ชนะ",sM_0b, "'Double Down'.",sM_0c,
	"Ace value.",sM_0d, "Nothing.",M_Menu;

sM_0a:
	mes "[Dealer]";
	mes "เป็นเกมส์ไพ่ที่แต้มสูงสุดคือ 21 หรือหาไพ่ผลรวมที่ใกล้เคียง 21";
	mes "โดยแต้มไม่สามารถสูงกว่า 21 ได้";
	next;
	mes "[Dealer]";
	mes "ก่อนจะเริ่มเกมส์ผู้เล่นจะต้องวางเดิมพันก่อน";
	mes "หลังจากนั้นผู้เล่นและเจ้ามือจะเรียกไพ่ 2 ใบ";
	mes "จากนั้นทั้งคู่สามารถเลือกที่จะ ^5533FF'อยู่'^000000,";
	mes "หรือ ^5533FF'เรียก'^000000. ไพ่เพิ่มได้";
	next;
	mes "[Dealer]";
	mes "เมื่อผู้เล่นเลือกที่จะ 'อยู่', หมายความว่าผู้เล่นไม่ต้องการไพ่เพิ่มเติม";
	mes "หลังจากนั้นเจ้ามือจะสามารถเลือกที่จะ 'อยู่' หรือ 'เรียก' ไพ่เพิ่มเติมได้";
	mes "ผู้เล่นจำเป็นที่จะต้องมีแต้ม ^FF3355อย่างน้อย 13 แต้ม^000000 เพื่อที่จะ 'อยู่'";
	mes "ในขณะเดียวกันเจ้ามือต้องมี่แต้ม ^FF3355อย่างน้อย 17 points^000000. เพื่อที่จะ 'อยู่'";
	next;
	mes "[Dealer]";
	mes "เมื่อผู้เล่นเลือกที่จะ 'เรียก' หมายความว่าผู้เล่นต้องการไพ่เพิ่มเติม";
	mes "เพื่อที่จะเพิ่มผลรวมแต้มในมือ";
	goto sM_Menu0;
sM_0b:
	mes "[Dealer]";
	mes "มี 3 วิธีที่จะแพ้ชนะ Blackjact";
	next;
	mes "[Dealer]";
	mes "1.) ผู้ที่มีแต้มรวมมากกว่าเป็นผู้ชนะ";
	next;
	mes "[Dealer]";
	mes "2.) ผู้เล่นเรียกไพ่ผลรวมสูงกว่า 21 จะถือว่าล้มละลาย จะส่งผลให้อีกฝ่ายชนะในทันที";
	next;
	mes "[Dealer]";
	mes "3.) ผู้ที่มีผลรวมเป็น 21 ในรอบไพ่ 2 ใบเรียกว่า ^5533FF'Black Jack'^000000";
	mes "กรณีจะเกิดขึ้นเมื่อ 'Ace' และ ไพ่ที่มี '10' แต้มในมือ";
	mes "ในกรณีที่เจ้ามือได้ Black Jack จะถือว่าชนะในรอบนั้นทันที";
	next;
	mes "[Dealer]";
	mes "4.) นอกเหนือจากการ แพ้ชนะ สามารถมีกรณี 'ชน' ได้";
	mes "ถ้าหากผู้เล่นและเจ้ามือมีแต้มที่เท่ากัน ซึ่งจะไม่ ได้/เสีย เงินในรอบนั้นๆ";
	next;
	mes "[Dealer]";
	mes "กรณี ^5533FF'ชน'^000000 สามารถเกิดได้กับ Black Jack เช่นกัน";
	mes "time.";
	goto sM_Menu0;
sM_0c:
	mes "[Dealer]";
	mes "'Double Down' เป็นการเรียกไพ่เพียง 1 ใบ และวางเงินเดิมพันเพิ่มเป็น 2 เท่า";
	goto sM_Menu0;
sM_0d:
	mes "[Dealer]";
	mes "'Ace' มีค่าเป็น 11 ซึ่งถือว่าเป็นไพ่ที่มีแต้มสูงสุดในเกม";
	mes "นอกเหนือจากนั้น Ace เป็นไพ่ที่มี 2 ค่าคือ 11 และ 1";
	goto sM_Menu0;
M_End:
	mes "[Dealer]";
	mes "เจ้ามาหาข้าได้ทุกเมื่อ";
	close;

//================
L_Play:
	mes "[Dealer]";
	mes "กรุณาเลือกจำนวนเงินเดิมพัน...";
	next;
	menu "500k",M_1a, "1m",M_1b, "2m",M_1c, "5m",M_1d, "เดิมพันน้อยเกิน ข้าไม่เล่น",M_End;

M_1a:
	if(Zeny < 500000) goto sL_NotEnuf;
	set @bet, 500000;
	set Zeny, Zeny - @bet;
	goto L_Cont0;
M_1b:
	if(Zeny < 1000000) goto sL_NotEnuf;
	set @bet, 1000000;
	set Zeny, Zeny - @bet;
	goto L_Cont0;
M_1c:
	if(Zeny < 2000000) goto sL_NotEnuf;
	set @bet, 2000000;
	set Zeny, Zeny - @bet;
	goto L_Cont0;
M_1d:
	if(Zeny < 5000000) goto sL_NotEnuf;
	set @bet, 5000000;
	set Zeny, Zeny - @bet;
	goto L_Cont0;

sL_NotEnuf:
	mes "[Dealer]";
	mes "ไอกระจอก เจ้ามีเงินไม่เพียงพอ";
	close;

L_Cont0:
	mes "(กำลังแจกไพ่ ...)";
	next;
	deletearray $@card[0],13;
	set @dealerTurn, 0;
	set @numP, 0;
	set @numD, 0;
	set @pAce, 0;
	set @dAce, 0;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;

//==============
L_Start:
	callsub sF_GetTot;

	mes "- ไพ่ของ ^FF5533DEALER'S^000000 คือ:";
	if (@numD==2) callsub sF_D2cards;
	if (@numD==3) callsub sF_D3cards;
	if (@numD==4) callsub sF_D4cards;
	if (@numD==5) callsub sF_D5cards;
	mes "    Dealer มีแต้มทั้งหมด: ^FF5533"+@dealTot+"^000000";
	mes " ";
	mes "- ไพ่ของ ^5533FFคุณ^000000 คือ:";
	if (@numP==2) callsub sF_P2cards;
	if (@numP==3) callsub sF_P3cards;
	if (@numP==4) callsub sF_P4cards;
	if (@numP==5) callsub sF_P5cards;
	if(@pAce != 1 || @playTot == 21) mes "    ผู้เล่นมีแต้มทั้งหมด: ^5533FF" +@playTot+ "^000000";
	if(@pAce == 1 && @playTot != 21) mes "    ผู้เล่นมีแต้มทั้งหมด: ^5533FF" +@playTot+ "^000000, หรือ ^5533FF" +(@playTot-10)+ "^000000";
	next;
	if(@playTot==21 && @dealTot==21) goto sL_Push;
	if(@numP==2 && @playTot == 21) goto sL_Win;
	if(@numD==2 && @dealTot == 21) goto sL_Lose;
	if(@playTot > 21) goto sL_Lose;
	if(@dealTot > 21) goto sL_Win;
	if(@numP==2 && @dealerTurn==0) menu "เรียก",M_Hit, "อยู่",M_Stay, "Double Down",M_Double;
	if(@dealerTurn == 0) menu "เรียก",M_Hit, "อยู่",M_Stay;

M_Stay:
	mes "[Dealer]";
	if(@playTot < 13) goto sL_PlayToLow;
	if(@dealTot > 16) mes "เจ้ามืออยู่.";
	if(@dealTot > 16 || @numD == 5) goto L_Check;
	mes "เจ้ามือกำลังจั่วเพิ่ม";
	next;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	set @dealerTurn, 1;
	goto L_Start;

sL_PlayToLow:
	mes "เจ้ามีแต้มไม่เพียงพอที่จะอยู่ จำเป็นต้องเรียกเพิ่ม.";
	next;
	goto M_Hit;

M_Hit:
	if(@numP == 5) goto M_Stay;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;
M_Double:
	mes "[Dealer]";
	mes "ผู้เล่นเลือกที่จะ Double Down! จำนวนเงินเดิมพันเพิ่มขึ้น 2 เท่า";
	mes "เจ้ามีสิทธิเรียกเพิ่มได้ 1 ใบเท่านั้น";
	next;
	if(Zeny < @bet) {
		mes "ผู้เล่นเงินไม่เพียงพอสำหรับ Double Down";
		goto L_Start;
	}
	set Zeny, Zeny - @bet;
	set @dealerTurn, 1;
	set @bet, @bet*2;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;

//=============
L_Check:
	next;
	if(@playTot < @dealTot) goto sL_Lose;
	if(@playTot == @dealTot) goto sL_Push;

sL_Win:
	mes "[Dealer]";
	mes "ยินดีด้วย! ผู้เล่นฟลุคชนะ";
	next;
	set Zeny, Zeny + (@bet * 2);
	announce "[Blackjack]: ผู้เล่น [" + strcharinfo(0) + "] ชนะ เดิมพัน Blackjack จำนวน" + @bet + "zeny."
	goto L_Play;
sL_Lose:
	mes "[Dealer]";
	mes "เสียใจด้วย วันนี้ไม่ใช่วันของเจ้า";
	announce "[Blackjack]: ผู้เล่น [" + strcharinfo(0) + "] แพ้ เดิมพัน Blackjack จำนวน" + @bet + "zeny."
	next;
	goto L_Play;
sL_Push:
	mes "[Dealer]";
	mes "เสมอ! ผู้เล่นและเจ้ามือชน";
	next;
	goto L_Play;


//==================================
	// Sub function for dealing/pulling the cards
sF_GetCards:
	set @rnd, rand(1,13);
	if($@card[@rnd] == 4) goto sF_GetCards;
	set $@card[@rnd], $@card[@rnd] + 1;
	set getarg(1), @rnd;
	if(getarg(1) > 10) set getarg(1), 10;
	if(getarg(1) == 1 && getarg(3) < 1) set getarg(1), 11;
	if(getarg(1) == 11) set getarg(3), 1;
	set getarg(2), " " + getarg(1) + " ";
	if(@rnd == 10) set getarg(2), getarg(1);
	if(@rnd == 1) set getarg(2), " A ";
	if(@rnd == 11) set getarg(2), " J ";
	if(@rnd == 12) set getarg(2), " Q ";
	if(@rnd == 13) set getarg(2), " K ";
	set getarg(0), getarg(0) + 1;
	return;

//==================================
	// Sub function for getting the total score for each hand
sF_GetTot:
	set @i, 0;
	set @dealTot, 0;
	set @playTot, 0;

GetDealTot:
	set @dealTot, @dealTot + @dealCard[@i];
	set @i, @i + 1;
	if(@i < @numD) goto GetDealTot;
	if(@dAce == 1 && @dealTot > 21) set @dealTot, @dealTot - 10;
	set @i, 0;
GetPlayTot:
	set @playTot, @playTot + @playCard[@i];
	set @i, @i + 1;
	if(@i < @numP) goto GetPlayTot;
	if(@pAce == 1 && @playTot > 21) set @pAce, 2;
	if(@pAce > 0 && @playTot > 21) set @playTot, @playTot - 10;
	return;

//=======================================
	// Sub function for displaying the Cards
sF_D2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_D3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" |   | "+@dealCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_P3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" |   | "+@playCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_D4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@dealCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_P4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" |";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@playCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_D5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[3]+" |   | "+@dealCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[3]+" |   | "+@playCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;

	OnInit:
	waitingroom "Blackjack",0;
	end;
}
