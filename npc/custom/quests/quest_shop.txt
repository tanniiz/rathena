//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
// prontera,164,203,6	script	Quest Shop#1	998,{ callfunc "qshop"; }
// prontera,165,203,6	script	Quest Shop#2	998,{ callfunc "qshop",1,2; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,0;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,5000;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#MobPoint", "Mob Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"MobPointShop",	// Shop Named 1
		"Ores",		// Shop Named 2
		"Other",
		"Figures",		// Shop Named 3
		"Hatter",
		"MVPCostume",
		"Weapon";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//		<Zeny cost>,<Mob point cost>, <chance>,
//		<required item ID>,<required item amount>,
//		[...<required item ID>,<required item amount>]);
// -----------------------------------------------------------

// Shop 1
	Add(1,60500,1,0,1,100);
  Add(1,985,1,100,1000,100);
  Add(1,984,1,100,1000,100);
	Add(1,7619,1,100,20000,100);
  Add(1,7620,1,100,20000,100);
	Add(1,6635,1,100,10000,100);
  Add(1,60000,1,100,2000,100);
  Add(1,12103,1,100000,500,60,604,30);

// Shop 2
	Add(2,757,5,100,0,100,985,1);
	Add(2,985,1,100,0,100,757,5);
	Add(2,756,5,100,0,100,984,1);
	Add(2,984,1,100,0,100,756,5);

// Shop 3
	Add(3,505,1,200,0,100,510,2,713,1);
	Add(3,504,1,100,0,100,509,2,713,1);
	Add(3,531,1,3,0,100,512,1,713,1);
	Add(3,532,1,3,0,100,513,1,713,1);
	Add(3,533,1,3,0,100,514,1,713,1);
	Add(3,534,1,3,0,100,515,1,713,1);

// Shop 4
	Add(4,69100,1,1500000,1000,30,2766,1,984,30,985,30);
	Add(4,69200,1,1500000,1000,30,2770,1,984,30,985,30);
	Add(4,69300,1,1500000,1000,30,2771,1,984,30,985,30);
	Add(4,69400,1,1500000,1000,30,2769,1,984,30,985,30);
	Add(4,69500,1,1500000,1000,30,2768,1,984,30,985,30);
	Add(4,69600,1,1500000,1000,30,2767,1,984,30,985,30);

// Shop 5
// Headgear A
	Add(5,5086,1,500000,0,100,2288,1,1095,3000);
	Add(5,5153,1,300000,0,100,5077,1,4001,1);
	Add(5,5074,1,20000,0,100,2254,1,2286,1);
	Add(5,2284,1,0,0,100,923,20);

// Headgear B
	Add(5,5042,1,0,0,100,10007,1,968,50);
	Add(5,5076,1,0,0,100,2226,1,7036,15);
	Add(5,5382,1,1000000,0,100,10016,1,2244,3);
	Add(5,2296,1,50000,0,100,2243,1,999,100);
	Add(5,5129,1,0,0,100,5189,1,7441,1);
	Add(5,5057,1,10000,0,100,2213,1,983,1,914,200);
	Add(5,5102,1,0,0,100,7263,50,2276,1);
	Add(5,5065,1,0,0,100,624,1,959,300,544,50,1023,1,938,100,7030,1);
	Add(5,5052,1,0,0,100,2211,1,978,1,7003,300);
	Add(5,5040,1,0,0,100,7047,100);
	Add(5,5016,1,0,0,100,1030,10);
	Add(5,2214,1,0,0,100,949,100,706,1,722,5,2213,1);

// Headgear C
	Add(5,5081,1,40000,0,100,2249,1,714,1,969,5);
	Add(5,5107,1,0,0,100,519,50,7031,50,548,50,539,50);
	Add(5,5075,1,0,0,100,2248,1,7030,108,7194,108,7120,4);
// Headgear D
	Add(5,5091,1,20000,0,100,10016,1,714,1,969,5);
	Add(5,5082,1,0,0,100,921,300);
	Add(5,2273,1,3500,0,100,2275,1,998,50,733,1);
	Add(5,5198,1,0,0,100,949,100,706,1,722,1,7212,1);
	Add(5,5038,1,500000,0,100,1038,1000,7048,80);
// Headgear E
	Add(5,5068,1,20000,0,100,2255,1,2286,1);
	Add(5,2283,1,10000,0,100,724,5,5001,1,949,300);
// Headgear F
	Add(5,5189,1,100000,0,100,925,200,7115,10);
	Add(5,5018,1,500,0,100,2274,1,916,300);
	Add(5,5380,1,0,0,100,951,100,961,50,1054,20,963,50,956,30,1023,100);
// Headgear G
	Add(5,5444,1,50000,0,100,2227,5,999,100,985,100);
	Add(5,5335,1,50000,0,100,741,500,4001,20);
// Headgear H
	Add(5,5025,1,1000000,0,100,2229,1,2254,1,7036,5);
	Add(5,5001,1,30000,0,100,999,40,984,10,970,2,1003,2);
	Add(5,5094,1,3000000,0,100,968,100,931,10000,909,10000);
// Headgear K
	Add(5,5020,1,0,0,100,2210,1,603,20,999,50,985,50,984,50);
	Add(5,5069,1,0,0,100,1022,999);
// Headgear M
	Add(5,5045,1,0,0,100,2252,1,1054,450,943,1200);
	Add(5,2278,1,5000,0,100,705,10,909,10,914,10);
// Headgear O
	Add(5,2281,1,10000,0,100,998,50,707,1);
// Headgear P
	Add(5,2293,1,10000,0,100,1049,10);
// Headgear R
	Add(5,5083,1,300000,0,100,2244,1,2209,1,10007,1);
// Headgear S
	Add(5,2280,1,5000,0,100,1019,100);
	Add(5,5032,1,0,0,100,1059,250,2221,1,2227,1,7063,500);
// Headgear T
	Add(5,5059,1,0,0,100,5030,1,603,20,999,100,985,50,984,50);
	Add(5,5077,1,0,0,100,2278,1,975,1);
// Headgear W
	Add(5,2292,1,300000,0,100,617,5,999,100,985,50,984,50);
// Dye
	Add(5,975,1,3000,0,100,507,30,973,1,7134,1);
	Add(5,976,1,3000,0,100,508,30,973,1,7134,1);
	Add(5,978,1,3000,0,100,510,30,973,1,7134,1);
	Add(5,979,1,5000,0,100,510,5,511,20,508,20,973,20,974,1,7134,1);
	Add(5,980,1,5000,0,100,507,20,508,20,973,1,974,1,7134,1);
	Add(5,981,1,5000,0,100,507,30,510,10,973,1,974,1,7134,1);
	Add(5,982,1,5000,0,100,509,30,973,1,7134,1);
	Add(5,983,1,5000,0,100,507,30,508,30,511,30,973,1,974,1,7134,1);
// Mixture and Counteragent
	Add(5,974,1,15000,0,100,970,10,972,10,713,50);
	Add(5,973,1,15000,0,100,970,10,971,10,713,50);

	// Shop 6: Costume Mvp
	Add(6,68000,1,5000000,10000,60,2254,1,984,100,985,100);
	Add(6,68001,1,3000000,5000,45,2235,1,984,50,985,50);
	Add(6,68002,1,3000000,5000,45,2256,1,984,50,985,50);
	Add(6,68003,1,3000000,5000,45,5019,1,984,50,985,50);
	Add(6,68004,1,3000000,5000,45,2234,1,984,50,985,50);
	Add(6,68005,1,3000000,5000,45,2249,1,984,50,985,50);
	Add(6,68006,1,3000000,5000,45,2258,1,984,50,985,50);
	Add(6,68009,1,3000000,5000,45,5007,1,984,50,985,50);
	Add(6,68007,1,1500000,3000,45,2268,1,984,20,985,20);
	Add(6,68008,1,1500000,3000,45,5074,1,984,20,985,20);

	// Shop 7: Upgrade Weapon
	Add(7,68100,1,3000000,10000,25,1127,3,984,20,985,50,999,100);
	Add(7,68101,1,3000000,10000,25,1255,3,984,20,985,20,999,100);
	Add(7,68102,1,3000000,10000,25,1304,3,984,20,985,20,999,100);
	Add(7,68103,1,3000000,10000,25,1716,3,984,20,985,20,999,100);
	Add(7,68104,1,3000000,10000,25,1611,3,984,20,985,20,999,100);
	Add(7,68105,1,3000000,10000,25,1611,3,984,20,985,20,999,100);

// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "กรุณาเลือกซื้อสินค้า 1 ชนิดต่อครั้ง";

	callshop "qshop"+@shop_index,1;
	npcshopattach "qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));

	set .@q[2],.@q[1]*.@q[3];
	if (.@q[6] != 100 && @bought_quantity[0] > 1) {
		mes "[Dealer]";
		mes "ไม่สามารถสร้างไอเทมหลายชิ้นพร้อมกันได้";
		close;
	}

	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Dealer]";
	mes "สินค้า: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	if (.@q[5] > 0) {
		mes "แต้ม " + .Points$[1] + " สะสม ^0055FF[" + getd(.Points$[0]) + "]^000000";
	}
	mes "เงื่อนไข:";
	disable_items;
	if (.@q[6] != 100) mes " > โอกาสสำเร็จ: ^FF0000"+.@q[6]+"% (ไม่รวม Research Tool)^000000";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[7]) for(set .@i,7; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0], ITEMINFO_LOCATIONS), getiteminfo(.@q[0], ITEMINFO_VIEW);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" แลกเลย! ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777ยกเลิก^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Dealer]";
				mes "ไสหัวไป! หาของที่ข้าต้องการให้ครบก่อนค่อยกลับมาหาข้า!";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Dealer]";
				mes "ไปจัดการน้ำหนักในตัวเจ้าเสียก่อน! ค่อยกลับมาหาข้า";
				close;
			}
  		.@useResearchTool = false;
			if (.@q[6] != 100) {
				.@researchToolCount = countitem(60000);

        if (.@researchToolCount > 0) {
          mes "[Dealer]";
          mes "Research Tool สามารถเพิ่มโอกาสสำเร็จได้ 15% ท่านต้องการใช้หรือไม่ ?";
          next;
          switch(select("แน่นอนสิ!:ไม่ละ ข้าห้าว")) {
            case 1:
              .@useResearchTool = true;
              .@q[6] += 15;
              break;
            case 2:
              break;
          }
        }
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[7]) {
				for(set .@i,7; .@i<getarraysize(.@q); set .@i,.@i+2) {
					delitem .@q[.@i],.@q[.@i+1]*.@q[1];
				}
			}
			if (.@useResearchTool) {
				delitem 60000,1;
			}

			if (rand(0, 100) <= .@q[6]) {
				getitem .@q[0],.@q[2];
				
				if (.@q[6] < 100) {
					announce "[Crafter] ปิ๊งป่อง!~ ยินดีด้วย คุณ [" + strcharinfo(0) + "] สร้าง [" + getitemname(.@q[0]) + "] สำเร็จ (โอกาสำเร็จ: " + .@q[6] + "%)",0;
				}
			} else {
				announce "[Crafter] ปิ๊งป่อง!~ เสียใจด้วย คุณ [" + strcharinfo(0) + "] สร้าง [" + getitemname(.@q[0]) + "] ไม่สำเร็จ (โอกาสำเร็จ: " + .@q[6] + "%)",0;
			}
			
			if (.@q[5]) {
				dispbottom "คุณมี " + .Points$[1] + " คงเหลือ " + getd(.Points$[0]);
			}
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4),getarg(5);
	for(set .@i,6; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0), ITEMINFO_TYPE) == 4 || getiteminfo(getarg(0), ITEMINFO_TYPE) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1
-	shop	qshop7	-1,909:-1
-	shop	qshop8	-1,909:-1

// Ores
morocc,43,54,6	script	Blacksmith#Ores	726,{
	callfunc "qshop", 2;

	OnInit:
	waitingroom "รวมแร่ต่าง ๆ",0;
	end;
}

// Juices
morocc,150,107,6	script	Juice Maker#Juice	612,{
	callfunc "qshop", 3;

	OnInit:
	waitingroom "น้ำผลไม้ดับร้อน",0;
	end;
}

// Mob Point
morocc,171,102,4	script	Monster Point#1	998,{
	callfunc "qshop", 1;

	OnInit:
	waitingroom "Mob Points",0;
	end;
}

// Figures
morocc,168,108,4	script	Figures Crafter#1	56,{
	callfunc "qshop", 4;

	OnInit:
	waitingroom "Figures Craft",0;
	end;
}

// Figures
morocc,177,140,6	script	Headgear Crafter#1	673,{
	callfunc "qshop", 5;

	OnInit:
	waitingroom "Hat Quests",0;
	end;
}

// Costume Mvp
morocc,142,140,6	script	MVP Crafter#1	673,{
	callfunc "qshop", 6;

	OnInit:
	waitingroom "MVP Costume Quests",0;
	end;
}

// Weapon
morocc,165,112,6	script	Weapon Crafter#1	84,{
	callfunc "qshop", 7;

	OnInit:
	waitingroom "Weapon Quests",0;
	end;
}