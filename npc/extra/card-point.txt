morocc,147,97,4	script	Card Point	573,{
	mes "[ Card Point ]";
	mes "สวัสดีคุณ,"+strcharinfo(0)+"";
	mes "คุณมีแต้มทั้งหมด : "+getd(.Points$)+" Points";
	mes "ต้องการแลกของรางวัลหรือไม่?";
	next;
	switch(select("-->อ่านรายละเอียด:-->เริ่มย่อยการ์ด:-->แลกของรางวัล:-->ยกเลิก")) {
	case 1:
		mes "ท่านสามารถนำการ์ดที่ไม่ได้ใช้";
		mes "นำมาแลกการ์ดเป็นไอเท็ม";
		mes "กับฉันได้ตลอด ทุกเวลาที่ต้องการ";
		mes "แต่ละใบจะได้แต้ม = 1 แต้ม";
		next;
		mes "การ์ดทุกใบที่ท่านนำมาแลก";
		mes "ท่านจะได้ "+.Points[0]+" Point"+((.Points[0] == 1)?"":"s")+"ต่อการ์ด";
		mes "ท่านสามารถเอาพ้อยจากแลกการ์ด";
		mes "มาซื้อสินค้าใน Shop ของฉันได้";
		mes "อย่าลืมรีบมาแลกล่ะ ?";
		emotion ET_MONEY;
		close;
		
	case 2:
		deletearray @sold_nameid[0],getarraysize(@sold_nameid);
		close2;
		callshop "card_shop",2;
		npcshopattach "card_shop";
		end;
	case 3:
		mes "[ CardPoint ]";
		mes "คุณมีแต้มทั้งหมด : "+getd(.Points$)+" Point"+((getd(.Points$) == 1)?".":"s.");
		close2;
		callshop "card_shop",1;
		npcshopattach "card_shop";
		end;
	case 4:	
		mes "[ CardPoint ]";
		mes "ฮืมๆ แย่จัง";
		mes "ไว้เจอกันใหม่ ๆ นะ!";
		emotion ET_SLEEPY;
		close;
	}

OnSellItem:
	mes "[ CardPoint ]";
	mes "รายการไอเท็ม :";
	for(set .@i,0; .@i<getarraysize(@sold_nameid); set .@i,.@i+1)
		if (@sold_nameid[.@i] > 4000 && @sold_nameid[.@i] < 4700) {
			set .@card_id[getarraysize(.@card_id)], @sold_nameid[.@i];
			set .@card_amt[getarraysize(.@card_amt)], @sold_quantity[.@i];
			set .@mvp, compare(.MVP$,""+@sold_nameid[.@i]);
			mes ((.@mvp)?"":"")+@sold_quantity[.@i]+"x "+getitemname(@sold_nameid[.@i])+"";
			set .@card_total, .@card_total+(@sold_quantity[.@i]*((.@mvp)?.Points[1]:.Points[0]));
		}
	deletearray @sold_nameid[0], getarraysize(@sold_nameid);
	deletearray @sold_quantity[0], getarraysize(@sold_quantity);
	if (!.@card_id) {
		mes "ไม่พบไอเท็มการ์ดของคุณ !";
		emotion ET_SWEAT;
		close;
	}
	mes "จำนวนแต้มที่ได้รับ :";
	mes "^399e00"+.@card_total+" Point";
	mes "คุณที่ต้องการแลกการ์ดหรือไม่ ?";
	next;
	if(select("-->แน่นอน ฉันต้องการขายการ์ด :-->ไม่ล่ะขอบคุณ") == 2) {
		emotion ET_SCRATCH;
		close;
	}
	for(set .@i,0; .@i<getarraysize(.@card_id); set .@i,.@i+1)
		delitem .@card_id[.@i],.@card_amt[.@i];
	setd .Points$, getd(.Points$)+.@card_total;
	mes "ยินดีด้วยนะค่ะ ฉันส่งไอเท็มแล้ว !";
	mes "คุณสามารถซื้อของใน Shop ได้แล้ว";
	emotion ET_DELIGHT;
	close;

OnBuyItem:
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set .@cost, .@cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	if (.@cost > getd(.Points$)) {
		mes "คุณมีแต้มไม่มากพอที่จะแลก";
		mes "จึงไม่สามารถทำรายการได้ !";
		emotion ET_HUK;
	}
	else {
		mes "รายการแลกไอเท็ม :";
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			mes "  ^0000FF"+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+"";
		}
		mes "จำนวนพ้อย : ";
		mes "^3e9b00"+.@cost+" Points";
		setd .Points$, getd(.Points$)-.@cost;
		emotion ET_MONEY;
	}
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;
OnInit:
	waitingroom "[Card] แลกแต้ม", 0;
	set .Points$,"#Card_Points";	// Variable to store points.
	setarray .Shop[0],		// Card Shop items: <ID>,<point cost>
	30100,1,616,20;
//	616,7,41124,15,41125,15,41126,15,41127,15,41128,15,41129,15,41130,15;
	setarray .Points[0],1,1;	// Points per <normal card>,<MVP card>
	set .MVP$,			// List of MVP cards.
	  "4322,4326";
	
	npcshopdelitem "card_shop",909;
	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)
		npcshopadditem "card_shop",.Shop[.@i],.Shop[.@i+1];	
}
-	shop	card_shop	-1,909:-1












