-	script	Quest_Hat	-1,{ 

callfunc "hat_quest_shop"; 

OnInit:
	waitingroom	"~ แหล่งรวมเควส",0;
	end;
}

//============================================================
// Script Core
//============================================================

-	script	quest_shop	-1,{ 

function Add; 
function Chk; 
function Slot; 
function A_An;

OnInit:

	freeloop(1);

// ===========================================================
//  Configulator Shop
// ===========================================================

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,0;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,10000;	// Max number of quest items purchased at one time.

// ===========================================================
//  Points 
//	setarray .Points$[0],"<variable name>","<display name>";
// ===========================================================

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// ===========================================================
//  Shop Dummy Data
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// ===========================================================

	setarray .Shops$[1],"~ เควสหมวก","~ เควส สีย้อมผ้า","~ เควส Mixture and Counteragent";

// ===========================================================
//  Quest items
//	Add(<shop ID>,<reward ID>,<reward amount>,<Zeny cost>,<point cost>,<required item ID>,<required item amount>{,...});
// ===========================================================

// Headgear A
	Add(1,5086,1,500000,0,2288,1,1095,3000);
	Add(1,5153,1,300000,0,5077,1,4001,1);
	Add(1,5074,1,20000,0,2254,1,2286,1);
	Add(1,2284,1,0,0,923,20);

// Headgear B
	Add(1,5042,1,0,0,10007,1,968,50);
	Add(1,5076,1,0,0,2226,1,7036,15);
	Add(1,5382,1,1000000,0,10016,1,2244,3);
	Add(1,2296,1,50000,0,2243,1,999,100);
	Add(1,5129,1,0,0,5189,1,7441,1);
	Add(1,5057,1,10000,0,2213,1,983,1,914,200);
	Add(1,5102,1,0,0,7263,50,2276,1);
	Add(1,5065,1,0,0,624,1,959,300,544,50,1023,1,938,100,7030,1);
	Add(1,5052,1,0,0,2211,1,978,1,7003,300);
	Add(1,5040,1,0,0,7047,100);
	Add(1,5016,1,0,0,1030,10);
	Add(1,2214,1,0,0,949,100,706,1,722,5,2213,1);

// Headgear C
	Add(1,5081,1,40000,0,2249,1,714,1,969,5);
	Add(1,5107,1,0,0,519,50,7031,50,548,50,539,50);
	Add(1,5075,1,0,0,2248,1,7030,108,7194,108,7120,4);
// Headgear D
	Add(1,5091,1,20000,0,10016,1,714,1,969,5);
	Add(1,5082,1,0,0,921,300);
	Add(1,2273,1,3500,0,2275,1,998,50,733,1);
	Add(1,5198,1,0,0,949,100,706,1,722,1,7212,1);
	Add(1,5038,1,500000,0,1038,1000,7048,80);
// Headgear E
	Add(1,5068,1,20000,0,2255,1,2286,1);
	Add(1,2283,1,10000,0,724,5,5001,1,949,300);
// Headgear F
	Add(1,5189,1,100000,0,925,200,7115,10);
	Add(1,5018,1,500,0,2274,1,916,300);
	Add(1,5380,1,0,0,951,100,961,50,1054,20,963,50,956,30,1023,100);
// Headgear G
	Add(1,5444,1,50000,0,2227,5,999,100,985,100);
// Headgear H
	Add(1,5025,1,1000000,0,2229,1,2254,1,7036,5);
	Add(1,5001,1,30000,0,999,40,984,10,970,2,1003,2);
	Add(1,5094,1,3000000,0,968,100,931,10000,909,10000);
// Headgear K
	Add(1,5020,1,0,0,2210,1,603,20,999,50,985,50,984,50);
	Add(1,5069,1,0,0,1022,999);
// Headgear M
	Add(1,5045,1,0,0,2252,1,1054,450,943,1200);
	Add(1,2278,1,5000,0,705,10,909,10,914,10);
// Headgear O
	Add(1,2281,1,10000,0,998,50,707,1);
// Headgear P
	Add(1,2293,1,10000,0,1049,10);
// Headgear R
	Add(1,5083,1,300000,0,2244,1,2209,1,10007,1);
// Headgear S
	Add(1,2280,1,5000,0,1019,100);
	Add(1,5032,1,0,0,1059,250,2221,1,2227,1,7063,500);
// Headgear T
	Add(1,5059,1,0,0,5030,1,603,20,999,100,985,50,984,50);
	Add(1,5077,1,0,0,2278,1,975,1);
// Headgear W
	Add(1,2292,1,300000,0,617,5,999,100,985,50,984,50);
// Dye
	Add(5,975,1,3000,0,507,30,973,1,7134,1);
	Add(5,976,1,3000,0,508,30,973,1,7134,1);
	Add(5,978,1,3000,0,510,30,973,1,7134,1);
	Add(5,979,1,5000,0,510,5,511,20,508,20,973,20,974,1,7134,1);
	Add(5,980,1,5000,0,507,20,508,20,973,1,974,1,7134,1);
	Add(5,981,1,5000,0,507,30,510,10,973,1,974,1,7134,1);
	Add(5,982,1,5000,0,509,30,973,1,7134,1);
	Add(5,983,1,5000,0,507,30,508,30,511,30,973,1,974,1,7134,1);
// Mixture and Counteragent
	Add(6,974,1,15000,0,970,10,972,10,713,50);
	Add(6,973,1,15000,0,970,10,971,10,713,50);
// =============================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "hat_quest_shop"+.@i,909;
	}
	end;

OnMenu:

	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"เกิดข้อผิดพลาด.";
		end;
	}
	dispbottom "[ Diary Quest ] โปรดเลือกไอเท็มที่คุณต้องการทำเควส.";
	callshop "hat_quest_shop"+.@i,1;
	npcshopattach "hat_quest_shop"+.@i;
	end;

OnBuyItem:
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[ Diary Quest ]";
	mes "ไอเท็มที่เลือก:";
	mes "^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "ไอเท็มที่ต้องการ:";
	//message strcharinfo(0),"Item ID: "+.@q[0]+".";
	disable_items;
	if (.@q[4]) mes "- "+Chk(Zeny,.@q[4]*.@q[1])+( callfunc("F_InsertComma", .@q[4]*.@q[1] ))+" Zeny^000000";
	if (.@q[5]) mes "- "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes "- "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select("~ แลก ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?"~ ทดลองสวมใส่...":"")+":~ ^FF0000ไม่ล่ะ,ไอเท็มยังไม่ครบ^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[ Diary Quest ]";
				mes "คุณ... ไม่มีไอเท็มที่ต้องใช้แลกเปลี่ยน";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[ Diary Quest ]";
				mes "^FF0000กรุณา ลด น้ำหนักลงจำนวน "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" หน่วย เพื่อที่จะสำเร๊จเควสดังกล่าวได้.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce "[ ผู้เล่น : "+strcharinfo(0)+" ได้รับไอเท็ม "+((.@q[2] > 1)?.@q[2]+" จำนวน "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+" ea. จาก Diary Quest ]",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:

	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {

	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "hat_quest_shop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {

	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^0000FF";
}

function Slot {

	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {

	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "-> "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	hat_quest_shop	{

	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	hat_quest_shop1	-1,909:-1
-	shop	hat_quest_shop2	-1,909:-1
-	shop	hat_quest_shop3	-1,909:-1
-	shop	hat_quest_shop4	-1,909:-1
-	shop	hat_quest_shop5	-1,909:-1
-	shop	hat_quest_shop6	-1,909:-1
-	shop	hat_quest_shop7	-1,909:-1
-	shop	hat_quest_shop8	-1,909:-1
-	shop	hat_quest_shop9	-1,909:-1
-	shop	hat_quest_shop10	-1,909:-1
-	shop	hat_quest_shop11	-1,909:-1
-	shop	hat_quest_shop12	-1,909:-1
-	shop	hat_quest_shop13	-1,909:-1
-	shop	hat_quest_shop14	-1,909:-1
-	shop	hat_quest_shop15	-1,909:-1
-	shop	hat_quest_shop16	-1,909:-1
-	shop	hat_quest_shop17	-1,909:-1
-	shop	hat_quest_shop18	-1,909:-1
-	shop	hat_quest_shop19	-1,909:-1
-	shop	hat_quest_shop20	-1,909:-1
-	shop	hat_quest_shop21	-1,909:-1
-	shop	hat_quest_shop22	-1,909:-1
-	shop	hat_quest_shop23	-1,909:-1
-	shop	hat_quest_shop24	-1,909:-1
-	shop	hat_quest_shop25	-1,909:-1
-	shop	hat_quest_shop26	-1,909:-1

morocc,167,108,4	duplicate(Quest_Hat)	Headgear Quest	790